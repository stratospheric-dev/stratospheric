plugins {
  id 'org.springframework.boot' version '3.2.0'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'java'
}

group = 'dev.stratospheric'
version = 'final'
sourceCompatibility = '17'

repositories {
  mavenCentral()
}

ext {
  set('awsSpringVersion', '3.0.1')
  set('testcontainersVersion', '1.18.0')
  set('awsLambdaJavaEventsVersion', '3.11.2')
  set('awsJavaSdkCoreVersion', '1.12.481')
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3'
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs'
  implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'
  // AWS Java SDK v1/v2 agnostic library for type-safe access to the S3 event object
  implementation "com.amazonaws:aws-lambda-java-events:${awsLambdaJavaEventsVersion}"
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.testcontainers:localstack'
  testImplementation 'org.testcontainers:junit-jupiter'
  // Required to compile due to coupling between the LocalStack Testcontainers module and the AWS Java SDK v1
  testImplementation "com.amazonaws:aws-java-sdk-core:${awsJavaSdkCoreVersion}"
}

dependencyManagement {
  imports {
    mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${awsSpringVersion}"
    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
  }
}

bootRun {
  jvmArgs = [
    "-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005",
    "-Daws.profile=stratospheric",
  ]
}

test {
  useJUnitPlatform()
}

jar {
  enabled = false
}
